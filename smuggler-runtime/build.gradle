apply plugin: "java-library"
apply plugin: "kotlin"
apply from: "$rootDir/pablo.gradle"

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

  compileOnly files(java.nio.file.Paths.get(findSdkLocation(), "platforms", "android-$projectCompileSdkVersion", "android.jar"))
}

pablo {
  artifactName = "smuggler-runtime"
}

private String findSdkLocation() {
  def localProperties = new File(project.rootProject.rootDir, "local.properties")
  if (localProperties.exists()) {
    def properties = new Properties()
    localProperties.withReader {
      properties.load(it)
    }

    def sdkDir = properties.getProperty("sdk.dir")
    if (sdkDir != null) {
      return sdkDir
    }

    def androidDir = properties.getProperty("android.dir")
    if (androidDir != null) {
      return androidDir
    }
  }

  def androidHome = System.getenv("ANDROID_HOME") ?: System.getenv("ANDROID_PATH")
  if (androidHome == null) {
    throw new org.gradle.api.GradleException("Failed to locate android sdk location")
  }

  return androidHome
}
